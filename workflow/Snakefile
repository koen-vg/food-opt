# SPDX-FileCopyrightText: 2025 Koen van Greevenbroek
#
# SPDX-License-Identifier: GPL-3.0-or-later

import os
from pathlib import Path

from snakemake.utils import min_version

from validation import load_secrets_with_env_fallback, validate

min_version("9.0")

os.makedirs("data/downloads", exist_ok=True)


configfile: "config/default.yaml"


# Load and merge secrets from file or environment variables
secrets = load_secrets_with_env_fallback(Path.cwd())
config.setdefault("credentials", {}).update(secrets)


validate(config, project_root=Path.cwd())


# Include common configuration and helper functions first
include: "rules/common.smk"
include: "rules/retrieve.smk"
include: "rules/geography.smk"
include: "rules/health.smk"
include: "rules/animals.smk"
include: "rules/crops.smk"
include: "rules/water.smk"
include: "rules/model.smk"
include: "rules/plotting.smk"
include: "rules/analysis.smk"
include: "rules/consumer_values.smk"
include: "rules/optimal_taxes.smk"
include: "rules/yield_gaps.smk"
include: "rules/luc.smk"
include: "rules/documentation.smk"


wildcard_constraints:
    climate_model=r"[a-zA-Z0-9\-]+",
    period=r"[A-Z0-9]+",
    climate_scenario=r"[A-Z0-9]+",
    input_level=r"H|L",
    water_supply=r"[ri]",
    crop=r"[a-zA-Z0-9\-]+",
    item=r"[a-zA-Z0-9_-]+",
    scenario=r"[a-zA-Z0-9_]+",  # Scenario preset wildcard


ALL_RULE_TARGETS = [
    "results/{name}/solved/model_scen-default.nc",
    # maps (not objective-specific, in base plots directory)
    "results/{name}/plots/regions_map.pdf",
    "results/{name}/plots/resource_classes_map.pdf",
    "results/{name}/plots/food_consumption_comparison.pdf",
    "results/{name}/plots/food_consumption_comparison.csv",
    "results/{name}/plots/system_cost_comparison.pdf",
    "results/{name}/plots/system_cost_comparison.csv",
    "results/{name}/plots/relative_risk_curves.pdf",
    # results plots
    "results/{name}/plots/scen-default/crop_production.pdf",
    "results/{name}/plots/scen-default/resource_usage.pdf",
    "results/{name}/plots/scen-default/crop_production.csv",
    "results/{name}/plots/scen-default/food_production.csv",
    "results/{name}/plots/scen-default/consumption_balance.pdf",
    "results/{name}/plots/scen-default/objective_breakdown.pdf",
    "results/{name}/plots/scen-default/objective_breakdown.csv",
    "results/{name}/plots/scen-default/emissions_breakdown.pdf",
    "results/{name}/plots/scen-default/yll_global_by_cause.pdf",
    # "results/{name}/plots/scen-default/health_risk_map.pdf",
    # "results/{name}/plots/scen-default/health_risk_by_region.csv",
    # "results/{name}/plots/scen-default/health_baseline_map.pdf",
    # "results/{name}/plots/scen-default/health_baseline_by_region.csv",
    # objective-specific maps
    "results/{name}/plots/scen-default/crop_production_map.pdf",
    "results/{name}/plots/scen-default/crop_use_breakdown.pdf",
    "results/{name}/plots/scen-default/crop_use_breakdown.csv",
    "results/{name}/plots/scen-default/feed_breakdown.pdf",
    "results/{name}/plots/scen-default/feed_breakdown.csv",
    "results/{name}/plots/scen-default/food_group_slack.pdf",
    "results/{name}/plots/scen-default/food_group_slack.csv",
    "results/{name}/plots/scen-default/slack_overview.pdf",
    "results/{name}/plots/scen-default/slack_overview.csv",
    "results/{name}/plots/scen-default/water_balance.pdf",
    "results/{name}/plots/scen-default/water_balance.csv",
    "results/{name}/plots/scen-default/water_use_map.pdf",
    "results/{name}/plots/scen-default/water_use_by_region.csv",
    # "results/{name}/plots/scen-default/water_value_map.pdf",   # Current not working
    "results/{name}/plots/scen-default/food_consumption.pdf",
    "results/{name}/plots/scen-default/food_consumption_map.pdf",
    "results/{name}/plots/scen-default/food_consumption_baseline_map.pdf",
    # marginal damages analysis
    "results/{name}/plots/scen-default/marginal_ghg_global.pdf",
    "results/{name}/plots/scen-default/marginal_yll_global.pdf",
    "results/{name}/plots/scen-default/marginal_damages_global.csv",
]

# Solve all configured scenarios without triggering plotting targets.
SOLVE_ALL_SCENARIOS_TARGETS = [
    f"results/{{name}}/solved/model_scen-{scenario}.nc" for scenario in list_scenarios()
]

# Run analysis for all configured scenarios.
ANALYZE_ALL_SCENARIOS_TARGETS = [
    f"results/{{name}}/analysis/scen-{scenario}/{output}"
    for scenario in list_scenarios()
    for output in [
        "crop_production.csv",
        "ghg_intensity.csv",
        "health_impacts.csv",
    ]
]

# Dynamically add targets for all comparison scenarios
# This ensures all prerequisite files are generated for comparison plots
# comparison_scenarios is defined in plotting.smk (loaded above via include)
for scenario_suffix in comparison_scenarios:
    # Add solved model for food consumption comparison
    ALL_RULE_TARGETS.append(f"results/{{name}}/solved/model_{scenario_suffix}.nc")
    # Add objective breakdown for system cost comparison
    ALL_RULE_TARGETS.append(
        f"results/{{name}}/plots/{scenario_suffix}/objective_breakdown.csv"
    )


rule all:
    input:
        expand(ALL_RULE_TARGETS, name=[name]),


rule solve_all_scenarios:
    input:
        expand(SOLVE_ALL_SCENARIOS_TARGETS, name=[name]),


rule analyze_all_scenarios:
    input:
        expand(ANALYZE_ALL_SCENARIOS_TARGETS, name=[name]),
