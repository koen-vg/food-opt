# SPDX-FileCopyrightText: 2025 Koen van Greevenbroek
#
# SPDX-License-Identifier: GPL-3.0-or-later

"""Rules for generating documentation figures.

These figures are git-tracked and used in the Sphinx documentation.
They use a coarser resolution configuration for faster generation
and to showcase global coverage.
"""

from glob import glob

# Documentation figures are generated using the doc_figures config
DOC_FIG_NAME = "doc_figures"

# List of all documentation figures to generate
DOC_FIGURES = [
    # Introduction figures
    "intro_global_coverage",
    "model_topology",
    # Land use figures
    "land_resource_classes",
    "environment_luc_inputs",
    "environment_luc_lef",
    "grazing_only_land_fraction",
    # Crop production figures
    "crop_yield_wheat",
    "crop_yield_wetland-rice",
    "crop_yield_maize",
    "crop_yield_resource_class_wheat",
    "multi_cropping_potential_rainfed",
    "multi_cropping_potential_irrigated",
    # Water availability figures
    "water_basin_availability",
    "water_region_availability",
    "irrigated_land_fraction",
    # Livestock figures
    "grassland_yield",
    # Trade figures
    "trade_network",
    # Workflow figures
    "workflow_rulegraph",
    # Analysis figures
    "analysis_marginal_ghg",
    "analysis_marginal_yll",
    # Health figures
    "health_clusters",
]


rule doc_fig_intro_global_coverage:
    """Generate global coverage map showing all modeled regions."""
    input:
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
    output:
        svg="docs/_static/figures/intro_global_coverage.svg",
        png="docs/_static/figures/intro_global_coverage.png",
    log:
        "logs/shared/doc_fig_intro_global_coverage.log",
    script:
        "../scripts/doc_figures/intro_global_coverage.py"


rule doc_fig_model_topology:
    """Generate high-level model topology diagram showing material flows."""
    input:
        model=f"results/{DOC_FIG_NAME}/build/model_scen-default.nc",
    output:
        svg="docs/_static/figures/model_topology.svg",
        png="docs/_static/figures/model_topology.png",
    log:
        "logs/shared/doc_fig_model_topology.log",
    script:
        "../scripts/visualize_model_topology.py"


rule doc_fig_land_resource_classes:
    """Generate map showing resource class stratification."""
    input:
        classes=f"processing/{DOC_FIG_NAME}/resource_classes.nc",
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
    output:
        svg="docs/_static/figures/land_resource_classes.svg",
        png="docs/_static/figures/land_resource_classes.png",
    log:
        "logs/shared/doc_fig_land_resource_classes.log",
    script:
        "../scripts/doc_figures/land_resource_classes.py"


rule doc_fig_environment_luc_inputs:
    """Visualise LUC carbon input datasets used in the model."""
    input:
        lc_masks=f"processing/{DOC_FIG_NAME}/luc/lc_masks.nc",
        agb=f"processing/{DOC_FIG_NAME}/luc/agb.nc",
        soc=f"processing/{DOC_FIG_NAME}/luc/soc.nc",
        regrowth="processing/shared/luc/regrowth_resampled.nc",
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
    output:
        svg="docs/_static/figures/environment_luc_inputs.svg",
        png="docs/_static/figures/environment_luc_inputs.png",
    log:
        "logs/shared/doc_fig_environment_luc_inputs.log",
    script:
        "../scripts/doc_figures/luc_inputs_map.py"


rule doc_fig_environment_luc_lef:
    """Visualise aggregated land-use change emission factors."""
    input:
        annualized=f"processing/{DOC_FIG_NAME}/luc/annualized.nc",
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
    output:
        svg="docs/_static/figures/environment_luc_lef.svg",
        png="docs/_static/figures/environment_luc_lef.png",
    log:
        "logs/shared/doc_fig_environment_luc_lef.log",
    script:
        "../scripts/doc_figures/luc_lef_map.py"


rule doc_fig_crop_yield:
    """Generate crop yield potential maps for selected crops."""
    input:
        yield_raster=lambda w: gaez_path("yield", "r", w.crop),
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
        conversions="data/yield_unit_conversions.csv",
    output:
        svg="docs/_static/figures/crop_yield_{crop}.svg",
        png="docs/_static/figures/crop_yield_{crop}.png",
    log:
        "logs/shared/doc_fig_crop_yield_{crop}.log",
    script:
        "../scripts/doc_figures/crop_yield_map.py"


rule doc_fig_crop_yield_resource_class:
    """Generate resource class yield comparison maps."""
    input:
        crop_yields=f"processing/{DOC_FIG_NAME}/crop_yields/{{crop}}_r.csv",
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
    output:
        svg="docs/_static/figures/crop_yield_resource_class_{crop}.svg",
        png="docs/_static/figures/crop_yield_resource_class_{crop}.png",
    params:
        resource_class_1=1,
        resource_class_2=2,
    log:
        "logs/shared/doc_fig_crop_yield_resource_class_{crop}.log",
    script:
        "../scripts/doc_figures/crop_yield_resource_class.py"


rule doc_fig_multi_cropping_potential_rainfed:
    """Visualise rain-fed multi-cropping zones and regional potential."""
    input:
        zone_raster=lambda w: gaez_path("multiple_cropping_zone", "r", "all"),
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
    output:
        svg="docs/_static/figures/multi_cropping_potential_rainfed.svg",
        png="docs/_static/figures/multi_cropping_potential_rainfed.png",
    params:
        water_supply="rainfed",
    log:
        "logs/shared/doc_fig_multi_cropping_potential_rainfed.log",
    script:
        "../scripts/doc_figures/multi_cropping_potential.py"


rule doc_fig_multi_cropping_potential_irrigated:
    """Visualise irrigated multi-cropping zones and regional potential."""
    input:
        zone_raster=lambda w: gaez_path("multiple_cropping_zone", "i", "all"),
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
    output:
        svg="docs/_static/figures/multi_cropping_potential_irrigated.svg",
        png="docs/_static/figures/multi_cropping_potential_irrigated.png",
    params:
        water_supply="irrigated",
    log:
        "logs/shared/doc_fig_multi_cropping_potential_irrigated.log",
    script:
        "../scripts/doc_figures/multi_cropping_potential.py"


rule doc_fig_water_basin_availability:
    """Generate basin water availability map."""
    input:
        basin_shapefile="data/downloads/Report53_Appendix/Report53-BlueWaterScarcity-ArcGIS-ShapeFile/Monthly_WS_GRDC_405_basins.shp",
        water_data=f"processing/{DOC_FIG_NAME}/water/blue_water_availability.csv",
    output:
        svg="docs/_static/figures/water_basin_availability.svg",
        png="docs/_static/figures/water_basin_availability.png",
    log:
        "logs/shared/doc_fig_water_basin_availability.log",
    script:
        "../scripts/doc_figures/water_basin_availability.py"


rule doc_fig_water_region_availability:
    """Generate regional water availability map."""
    input:
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
        water_data=f"processing/{DOC_FIG_NAME}/water/region_growing_season_water.csv",
    output:
        svg="docs/_static/figures/water_region_availability.svg",
        png="docs/_static/figures/water_region_availability.png",
    log:
        "logs/shared/doc_fig_water_region_availability.log",
    script:
        "../scripts/doc_figures/water_region_availability.py"


rule doc_fig_irrigated_land_fraction:
    """Generate irrigated land fraction map."""
    input:
        irrigated_fraction="data/downloads/gaez_land_equipped_for_irrigation_share.tif",
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
    output:
        svg="docs/_static/figures/irrigated_land_fraction.svg",
        png="docs/_static/figures/irrigated_land_fraction.png",
    log:
        "logs/shared/doc_fig_irrigated_land_fraction.log",
    script:
        "../scripts/doc_figures/irrigated_land_fraction.py"


rule doc_fig_grassland_yield:
    """Generate managed grassland yield map."""
    input:
        grassland_yield="data/downloads/grassland_yield_historical.nc4",
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
    output:
        svg="docs/_static/figures/grassland_yield.svg",
        png="docs/_static/figures/grassland_yield.png",
    log:
        "logs/shared/doc_fig_grassland_yield.log",
    script:
        "../scripts/doc_figures/grassland_yield_map.py"


rule doc_fig_grazing_only_land_fraction:
    """Visualise grazing-only land availability."""
    input:
        classes=f"processing/{DOC_FIG_NAME}/resource_classes.nc",
        lc_masks=f"processing/{DOC_FIG_NAME}/luc/lc_masks.nc",
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
        suitability=[gaez_path("suitability", "r", crop) for crop in config["crops"]],
    output:
        svg="docs/_static/figures/grazing_only_land_fraction.svg",
        png="docs/_static/figures/grazing_only_land_fraction.png",
    log:
        "logs/shared/doc_fig_grazing_only_land_fraction.log",
    script:
        "../scripts/doc_figures/grazing_only_land_fraction.py"


rule doc_fig_trade_network:
    """Generate trade network map showing hubs and links."""
    input:
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
    output:
        svg="docs/_static/figures/trade_network.svg",
        png="docs/_static/figures/trade_network.png",
    params:
        n_hubs=config["trade"]["crop_hubs"],
    log:
        "logs/shared/doc_fig_trade_network.log",
    script:
        "../scripts/doc_figures/trade_network_map.py"


rule doc_fig_workflow_rulegraph_dot:
    """Generate workflow dependency graph in DOT format from Snakemake."""
    output:
        dot="docs/_static/figures/workflow_rulegraph_raw.dot",
    log:
        "logs/shared/doc_fig_workflow_rulegraph_dot.log",
    shell:
        """
        snakemake --rulegraph --config name=test > {output.dot} 2> {log}
        """


rule doc_fig_workflow_rulegraph_styled:
    """Apply custom styling and text wrapping to DOT graph."""
    input:
        dot="docs/_static/figures/workflow_rulegraph_raw.dot",
    output:
        dot="docs/_static/figures/workflow_rulegraph.dot",
    log:
        "logs/shared/doc_fig_workflow_rulegraph_styled.log",
    script:
        "../scripts/doc_figures/style_workflow_graph.py"


rule doc_fig_workflow_rulegraph:
    """Render workflow dependency graph to SVG and PNG using Graphviz."""
    input:
        dot="docs/_static/figures/workflow_rulegraph.dot",
    output:
        svg="docs/_static/figures/workflow_rulegraph.svg",
        png="docs/_static/figures/workflow_rulegraph.png",
    log:
        "logs/shared/doc_fig_workflow_rulegraph.log",
    script:
        "../scripts/doc_figures/render_graph.py"


rule doc_fig_analysis_ghg_health:
    """Generate GHG and health impact bar charts for documentation."""
    input:
        ghg_intensity=f"results/{DOC_FIG_NAME}/analysis/scen-default/ghg_intensity.csv",
        health_marginals=f"results/{DOC_FIG_NAME}/analysis/scen-default/health_marginals.csv",
    output:
        ghg_svg="docs/_static/figures/analysis_marginal_ghg.svg",
        ghg_png="docs/_static/figures/analysis_marginal_ghg.png",
        yll_svg="docs/_static/figures/analysis_marginal_yll.svg",
        yll_png="docs/_static/figures/analysis_marginal_yll.png",
    log:
        "logs/shared/doc_fig_analysis_ghg_health.log",
    script:
        "../scripts/doc_figures/analysis_ghg_health.py"


rule doc_fig_health_clusters:
    """Generate health cluster map showing country groupings."""
    input:
        regions=f"processing/{DOC_FIG_NAME}/regions.geojson",
        clusters=f"processing/{DOC_FIG_NAME}/health/country_clusters.csv",
    output:
        svg="docs/_static/figures/health_clusters.svg",
        png="docs/_static/figures/health_clusters.png",
    log:
        "logs/shared/doc_fig_health_clusters.log",
    script:
        "../scripts/doc_figures/health_clusters_map.py"


rule build_docs:
    """Build Sphinx documentation including all figures."""
    input:
        # Figures
        expand("docs/_static/figures/{fig}.svg", fig=DOC_FIGURES),
        expand("docs/_static/figures/{fig}.png", fig=DOC_FIGURES),
        # Documentation source files
        "docs/conf.py",
        glob("docs/**/*.rst", recursive=True),
    output:
        "docs/_build/html/index.html",
    log:
        "logs/shared/build_docs.log",
    shell:
        """
        cd docs && make html > ../{log} 2>&1
        """
